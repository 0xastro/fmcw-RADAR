@echo off
SETLOCAL ENABLEEXTENSIONS
SETLOCAL ENABLEDELAYEDEXPANSION

REM
REM Prerequisite: Ensure that the MMWAVE_SDK_INSTALL_PATH & MMWAVE_SECDEV_INSTALL_PATH 
REM have been setup properly before calling the script.
REM
REM ================================================
REM generateHSMetaImage.bat <FLASHIMAGE> <SHMEM_ALLOC> <MSS_IMAGE_OUT> <BSS_IMAGE_BIN> <DSS_IMAGE_OUT> <HSIMAGE_CFG>
REM     where
REM         FLASHIMAGE:     [output] multicore HS file that will be generated by this script and should be used for flashing onto the board
REM         SHMEM_ALLOC:    [input]  shared memory allocation in 32-bit hex format where each byte (left to right) is for BSS,TCMB,TCMA,DSS
REM         MSS_IMAGE_OUT:  [input]  MSS input image in ELF (*.xer4f) format , use keyword NULL if not needed
REM         BSS_IMAGE_BIN:  [input]  BSS input image in RPRC (,bin) format, use keyword NULL if not needed
REM         DSS_IMAGE_OUT:  [input]  DSP input image in ELF (*.xe674) format, use keyword NULL if not needed
REM         HSIMAGE_CFG:    [input]  certificate and key related configuration file
REM
REM     example:
REM         generateHSMetaImage.bat xwr16xx_mmw_secure.bin 0x00000006 xwr16xx_mmw_demo_mss.xer4f xwr16xx_radarss_rprc.bin xwr16xx_mmw_demo_dss.xe674 hsimage.cfg
REM ================================================

REM ================================================
REM argument checking and parsing
if [%MMWAVE_SDK_DEVICE_TYPE%] == [xwr14xx] (
    echo Error: generateHSMetaImage not supported for xwr14xx !!!
    exit /b 1 
)
if [%MMWAVE_SECDEV_INSTALL_PATH%] == [] (
    @echo Info: MMWAVE_SECDEV_INSTALL_PATH is not defined. Skipping secure build
    REM This is a valid condition and hence exit the script gracefully
    goto:eof
)
echo Cmd line used: %0 %*
if [%MMWAVE_SDK_INSTALL_PATH%] == [] (
    @echo Error: MMWAVE_SDK_INSTALL_PATH [mmWave SDK Package Location] needs to be defined
    exit /b 1 
)

REM Calculate the number of arguments
SET /A ARGS_COUNT=0
FOR %%A in (%*) DO SET /A ARGS_COUNT+=1
if %ARGS_COUNT% lss 6 (
    echo Error: Invalid Usage. Insufficient parameters.
    echo        generateHSMetaImage.bat FLASHIMAGE SHMEM_ALLOC MSS_IMAGE_OUT BSS_IMAGE_BIN DSS_IMAGE_OUT HSIMAGE_CFG
    exit /b 1 
)

SET FLASHIMAGE=%1
SET SHMEM_ALLOC=%2
SET MSS_IMAGE_OUT=%3
SET BSS_IMAGE_BIN=%4
SET DSS_IMAGE_OUT=%5
SET HSIMAGE_CFG=%6

REM ================================================
REM tools required
SET HS_IMAGE_CREATOR=%MMWAVE_SECDEV_INSTALL_PATH%\hs_image_creator\hs_image_creator.exe
REM ================================================

REM ================================================
REM setup arguments for hs image creator
REM Following are mandatory parameters
SET HS_IMAGE_CREATOR_CMDLINE_ARGS=-o %FLASHIMAGE% -f %HSIMAGE_CFG% -m %MSS_IMAGE_OUT% -s %SHMEM_ALLOC%

REM add BSS to the cmd line if it is not NULL
if NOT "%BSS_IMAGE_BIN%" == "NULL" (
    SET HS_IMAGE_CREATOR_CMDLINE_ARGS=%HS_IMAGE_CREATOR_CMDLINE_ARGS% -b %BSS_IMAGE_BIN%
)

REM add DSS to the cmd line if it is not NULL
if NOT "%DSS_IMAGE_OUT%" == "NULL" (
    SET HS_IMAGE_CREATOR_CMDLINE_ARGS=%HS_IMAGE_CREATOR_CMDLINE_ARGS% -d %DSS_IMAGE_OUT%
)

REM generate secure multicore image
echo call %HS_IMAGE_CREATOR% %HS_IMAGE_CREATOR_CMDLINE_ARGS%
call %HS_IMAGE_CREATOR% %HS_IMAGE_CREATOR_CMDLINE_ARGS%

IF EXIST %FLASHIMAGE% (
    echo **************************************************************************
    echo Built the secure flash binary %FLASHIMAGE%    
    echo **************************************************************************
)

REM Find the MSS/DSS bin file names generated by hs_image_creator so that they can be deleted after the metaimage generation
IF EXIST %MSS_IMAGE_OUT% (
    call:makeBinFileName %MSS_IMAGE_OUT% MSS_IMAGE_BIN
    REM Please note use of !MSS_IMAGE_BIN! due to delayed expansion inside the "IF"
    IF EXIST !MSS_IMAGE_BIN! (del !MSS_IMAGE_BIN!)
)
IF EXIST %DSS_IMAGE_OUT% (
    call:makeBinFileName %DSS_IMAGE_OUT% DSS_IMAGE_BIN
    REM Please note use of !DSS_IMAGE_BIN! due to delayed expansion inside the "IF"
    IF EXIST !DSS_IMAGE_BIN! (del !DSS_IMAGE_BIN!)
)

goto:eof

REM --------------------------------------------------------
REM Function makeBinFileName takes the out file name as input 
REM and returns the same name with .bin extension in the second arg
REM --------------------------------------------------------

:makeBinFileName
SET filename=%~n1
SET ext=%~x1
REM echo Out file name is %filename% and extension is %ext%
IF [%ext%] == [bin] (
    echo "Error: %1 file passed for MSS/DSS. generateHSMetaImage should be passed out files(.xer4f/.xe674) for MSS/DSS and not .bin"
    exit /b 1
)
SET "%2=%filename%.bin"
goto:eof

